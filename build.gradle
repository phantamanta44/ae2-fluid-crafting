buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "1.0.2"
group = "xyz.phanta.ae2fc"
archivesBaseName = "ae2fc-1.12.2"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    useDepAts = true
}

repositories {
    maven { url = 'http://dvs1.progwml6.com/files/maven/' }
    maven { url 'https://jitpack.io/' }
}

dependencies {
    compile 'com.github.phantamanta44:jsr305:1.0.1'
    deobfCompile 'mezz.jei:jei_1.12.2:4.15.0.293'
    deobfCompile 'appeng:appliedenergistics2:rv6-stable-7'
}

jar {
    manifest {
        attributes 'FMLAT': 'ae2fc_at.cfg'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLCorePlugin': 'xyz.phanta.ae2fc.coremod.FcCoreMod'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // copy access transformers
    rename '.+_at\\.cfg', 'META-INF/$0'
}
